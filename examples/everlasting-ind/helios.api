// helios protocol with identities - no privacy because of ballot replay attack

symbols aenc/3, adec/2, break/1, pk/1, pair/2, fst/1, snd/1, sign/2, getmsg/1, check/2, ok/0, no/0, yes/0, a/0, b/0;

private ka, kb, skE, ra, rb;

channels auth3, ch;

evchannels bb;

var v, id, r, k, sig, x, x1, x2, y, z, z2;


// Rewrite rules asymmetric for encryption and pairing

rewrite adec(x,aenc(pk(x),y,z)) -> z;
rewrite fst(pair(x, y)) -> x;
rewrite snd(pair(x, y)) -> y;

// Everlasting rewrite rules for asymmetric encryption and pairing

evrewrite adec(x,aenc(pk(x),y,z)) -> z;
evrewrite break(aenc(pk(x),y,z)) -> z;
evrewrite fst(pair(x, y)) -> x;
evrewrite snd(pair(x, y)) -> y;


V = out(bb, pair(id, aenc(pk(skE),r,v))).0;

Ayes = let v = yes in let id = a  in let r = ra in V;
Ano  = let v = no  in let id = a in let r = ra in V;

Byes = let v = yes in let id = b in let r = rb in V;
Bno  = let v = no  in let id = b in let r = rb in V;


AyBn = Ayes || Bno;
AnBy = Ano  || Byes;

BB = in(auth3,z). out(bb, z);

T =  out(bb,yes) || out(bb,no) || out(bb, adec(skE, snd(z)));

P =  out(bb,pk(skE)) :: (AyBn || BB) :: T;
Q =  out(bb,pk(skE)) :: (AnBy || BB) :: T;



equivalentft? P and Q;
// privacy cannot be proven because of vote replay attack
