// KCL-like protocol, without private hash, and encoded without pairs.

#set xor;

symbols plus/2, zero/0, h/1;

private idA, idB, r2, r4, ok;
channels C;
var X, Y, Z;

// We model RFID tags that authenticate with a reader
// in the following way:
//   reader sends a nonce r1
//   tag A sends <idA+r2, h(r1)+r2>
// (in the real protocol there is also a shared key, but it
//  doesn't play an important role here)
// The protocol does not ensure untraceability: a fake
// reader can initiate two sessions with the same nonce
// and will be able to tell if the two sessions are against
// the same tag or not. Indeed, by xoring the two components
// of the pair one gets idX+h(r1).

TtagA1 = in(C,Y).out(C,plus(idA,r2)).out(C,plus(h(Y),r2)).0;
TtagA2 = in(C,X).out(C,plus(idA,r4)).out(C,plus(h(X),r4)).0;
TtagB2 = in(C,X).out(C,plus(idB,r4)).out(C,plus(h(X),r4)).0;

r1 = TtagA1 :: TtagA2;
r2 = TtagA1 :: TtagB2;

not equivalentct? r1 and r2;
