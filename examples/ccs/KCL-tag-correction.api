// RFID protocol KCL07 (fixed)
// Scenario: one honest session followed by TagA (resp. TagB)

#set xor;

symbols h/2, pair/2, fst/1, snd/1;
private r1,idA, idB, r2, r4, kA, kB;
channels C;
var X, Y, Z;

rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;

//  Alice-Bob notation
//   R --> T: r1
//   T --> R: <idA+r2, h(r1+r2,kA)>

// Tinit models the messages outputted during
// an honest session between the tag A and the reader.
// TtagA models the role of Tag played by A.
// TtagB models the role of Tag played by B.

Tinit = out(C,r1).out(C,pair(idA+r2,h(r1+r2,kA))).0;
TtagA = in(C,X).out(C,pair(idA+r4,h(X+r4,kA))).0;
TtagB = in(C,X).out(C,pair(idB+r4,h(X+r4,kB))).0;

Psame = Tinit :: TtagA;
Pdiff = Tinit :: TtagB;

equivalentft? Psame and Pdiff;
