// RFID protocol OTYT'06
//scenario: one honest session followed by TagA (resp. TagB) in parallel with a reader


#set xor;

symbols h/1;
private kA, kB, r1, r2, k1, k2;
channels C, CR;
var X,Y,Z;


//  Alice-Bob notation
// Reader and Tag know the key kX and its value will be updated an the end
// of each session
//   R --> T: r1
//   T --> R: h(kX + r1)
//   R --> T: kX + k1 (k1 fresh key)
//   both update their key kX with k1.

// Tinit models the messages outputted during an honest session between the tag A and the reader
// TtagA models the role of Tag played by A
// TtagB models the role of Tag played by B

// Note also that in process TtagA we use k1 instead of kA to model the fact that
// the key has been updated after the first session.

Treader = out(CR, r2).in(CR, Z).(([Z=h(k1+r2)].out(CR,k1+k2).0) ++ ([Z=h(kB+r2)].out(CR,kB+k2).0));
Tinit = out(C,r1).out(C,h(kA+r1)).out(C,kA+k1).0;
TtagA = in(C,X).out(C,h(k1+X)).in(C,Y).0;
TtagB = in(C,X).out(C,h(kB+X)).in(C,Y).0;

Psame = Tinit :: (TtagA || Treader);
Pdiff = Tinit :: (TtagB || Treader);

not equivalentct? Psame and Pdiff;

