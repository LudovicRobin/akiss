// RFID protocol KCL07
// scenario: one honest session followed by TagA (resp. TagB)

#set xor;

symbols h/2, pair/2, fst/1, snd/1;
private r1,idA, idB, r2, r4, kA, kB;
channels C;
var X, Y, Z;

rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;

//  Alice-Bob notation
//   R --> T: r1
//   T --> R: <idA+r2, h(r1,kA)+r2>


// Tinit models the messages outputted during an honest session between the tag A and the reader
// TtagA models the role of Tag played by A
// TtagB models the role of Tag played by B


Tinit = out(C,r1).out(C,pair(plus(idA,r2),plus(h(r1,kA),r2))).0;
TtagA = in(C,X).out(C,pair(plus(idA,r4),plus(h(X,kA),r4))).0;
TtagB = in(C,X).out(C,pair(plus(idB,r4),plus(h(X,kB),r4))).0;

Psame = Tinit :: TtagA;
Pdiff = Tinit :: TtagB;

not equivalentct? Psame and Pdiff;
