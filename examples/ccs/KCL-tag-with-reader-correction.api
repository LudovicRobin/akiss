// RFID protocol KCL07 (modified)
// Running example, modified to obtain unlinkability

#set xor;

symbols h/2, pair/2, fst/1, snd/1;
private idA, idB, r1, r2, r3, r4, kA, kB;
channels CR, C;
var W, X, Y, Z;

rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;

//  Alice-Bob notation
//   R --> T: r1
//   T --> R: <idA+r2, h(r1+r2,kA)>

// An attacker can initiate two sessions with the same nonce
// and will be able to tell if the two sessions are against
// the same tag or not. Indeed, by xoring the two components
// of the pair one gets idX+h(r1).

Treader1 = out(CR,r1).in(CR,W).([snd(W)=h(r1+idA+fst(W),kA)].out(CR,0).0 ++ [snd(W)=h(r1+idB+fst(W),kB)].out(CR,0).0);
Treader2 = out(CR,r3).in(CR,Z).([snd(Z)=h(r3+idA+fst(Z),kA)].out(CR,0).0 ++ [snd(Z)=h(r3+idB+fst(Z),kB)].out(CR,0).0);
TtagA1 = in(C,Y).out(C,pair(idA+r2,h(Y+r2,kA))).0;
TtagA2 = in(C,X).out(C,pair(idA+r4,h(X+r4,kA))).0;
TtagB2 = in(C,X).out(C,pair(idB+r4,h(X+r4,kB))).0;

Psame = (TtagA1 || Treader1):: (TtagA2 || Treader2);
Pdiff = (TtagA1 || Treader1):: (TtagB2 || Treader2);

equivalentft? Psame and Pdiff;
