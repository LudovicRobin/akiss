// RFID protocol LAK'06
// Authentication property
// P and Q are similar. They only differ by the fact that
// Q will emit an additional message "zero" if it executes until the end.
// If such a message equivalence will not hold and this will witness an authentication problem
// since reader will be able to execute (apparently with tag A) whereas tagA did not start any session.


#set xor;

symbols h/1,pair/2, fst/1, snd/1;
private kA, r0, r1, r2;
channels C;
var X,Y;

rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;


//  Alice-Bob notation
// R --> T: r0
// T --> R: <r1, h(r0+r1+k)>
// R --> T: h(h(r0+r1+k)+k+r0)
// R and T update their key k with h(k)

// Tinit models the two first messages outputted during an honest session between
// the reader and tag A. We assume that the reader did not reach the end of
// the session and therefore it did not update the key
// Treader models a session of reader talking to Tag A. The key that is used is kA
// We would like to know whether it is possible for the reader to reach the end
// of the session whereas TagA has not yet started.

Tinit = out(C,r0).out(C,pair(r1,h(plus(r0,plus(r1,kA))))).out(C,h(plus(plus(h(plus(r0,plus(r1,kA))),kA),r0))).0;
Treader = out(C,r2).in(C,X).[snd(X)=h(plus(r2,plus(fst(X),kA)))].0;

P = Tinit :: Treader;
Q = Tinit :: Treader :: out(C,zero);

not equivalentct? P and Q;

