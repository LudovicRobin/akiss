// RFID protocol KCL07
// Scenario: one honest session followed by TagA (resp. TagB)

#set xor;

symbols h/2, pair/2, fst/1, snd/1;
private r1,r2,r3,r4,idA, idB, kA, kB;
channels C,CR;
var  W, X, Y, Z;

rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;

//  Alice-Bob notation
//   R --> T: r1
//   T --> R: <idA+r2, h(r1,kA)+r2>

// Tinit models the messages outputted during an honest session
// between the tag A and the reader.
// TtagA models the role of Tag played by A.
// TtagB models the role of Tag played by B.

Treader1 = out(CR,r1).in(CR,W).([snd(W)+fst(W)=h(r1,kA)+idA].out(CR,0).0 ++ [snd(W)+fst(W)=h(r1,kB)+idB].out(CR,0).0);
Treader2 = out(CR,r3).in(CR,Z).([snd(Z)+fst(Z)=h(r3,kA)+idA].out(CR,0).0 ++ [snd(Z)+fst(Z)=h(r3,kB)+idB].out(CR,0).0);

TtagA1 = in(C,Y).out(C,pair(idA+r2,h(Y,kA)+r2)).0;
TtagA2 = in(C,X).out(C,pair(idA+r4,h(X,kA)+r4)).0;
TtagB2 = in(C,X).out(C,pair(idB+r4,h(X,kB)+r4)).0;

Psame = (TtagA1 || Treader1) :: (TtagA2 || Treader2);
Pdiff = (TtagA1 || Treader1) :: (TtagB2 || Treader2);

not equivalentct? Psame and Pdiff;
