// RFID protocol KCL07
// Running example

#set xor;

symbols h/2, pair/2, fst/1, snd/1;
private idA, idB, r2, r4, kA, kB;
channels C;
var X, Y;

rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;

//  Alice-Bob notation
//   R --> T: r1
//   T --> R: <idA+r2, h(r1,kA)+r2>

// An attacker can initiate two sessions with the same nonce
// and will be able to tell if the two sessions are against
// the same tag or not. Indeed, by xoring the two components
// of the pair one gets idX+h(r1).

TtagA1 = in(C,Y).out(C,pair(idA+r2,h(Y,kA)+r2)).0;
TtagA2 = in(C,X).out(C,pair(idA+r4,h(X,kA)+r4)).0;
TtagB2 = in(C,X).out(C,pair(idB+r4,h(X,kB)+r4)).0;

Psame = TtagA1 :: TtagA2;
Pdiff = TtagA1 :: TtagB2;

// print_traces Psame;

not includedct? Psame in Pdiff;
