// simplified version - without pair - of RFID protocol LAK 06
// cf. Survey Attacks on RFID protocols. T. Deursen and S. Radomirovic
// I do not understand the purpose of k0 known by the reader.
// According to the survey there is no attack on untraceability.

#set xor;

symbols h/1;
private kA, kB, k0, r0, r1, r2;
channels C;
var X,Y,Y2,Z2;

// The protocol works as follows:
// reader sends a fresh nonce r0
// tag sends <r1, h*r0+r1+k)>
// reader sends h(h(r0+r1+k)+k+r0)
// Then the reader and the tag update their key k with h(k)

// Tinit models an honest session between the reader and tag A that
// has been eavesdropped.
// TtagA2 models a session of Tag A. The key that is used is h(kA).
// TtagB2 models a session of Tag B. The key that is used is kB.

Tinit  = out(C,r0).
         out(C,r1).
         out(C,h(plus(r0,plus(r1,kA)))).
         out(C,h(plus(h(plus(r0,plus(r1,kA))),plus(kA,r0)))).0;

TtagA2 = in(C,Y2).
         out(C,r2).
         out(C,h(plus(Y2,plus(r2,h(kA))))).
         in(C,Z2).
         [Z2=h(plus(h(plus(Y2,plus(r2,h(kA)))),plus(h(kA),r0)))].0;

TtagB2 = in(C,Y2).
         out(C,r2).
         out(C,h(plus(Y2,plus(r2,kB)))).
         in(C,Z2).
         [Z2=h(plus(h(plus(Y2,plus(r2,kB))),plus(kB,r0)))].0;

r1 = Tinit :: TtagA2;
r2 = Tinit :: TtagB2;

equivalentct? r1 and r2;
