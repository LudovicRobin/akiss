symbols open/2, commit/2, check/2, sign/2, pk/1, unblind/2, blind/2,
 yes/0, no/0, kAuth/0;

private rAyes, bAyes, rAno, bAno, kA, kB, rBno, bBno, rByes, bByes;

channels A, B, C;

var x, y, z, xAyes, xBno, xByes, xAno;

rewrite open(commit(x, y), y) -> x;
rewrite check(sign(x, y), pk(y)) -> x;
rewrite unblind(sign(blind(x, y), z), y) -> sign(x, z);


AyesP1 = out(A, sign(blind(commit(yes, rAyes), bAyes), kA)).0;
AyesP2 = in(A, xAyes).0;
AyesP3 = [check(xAyes, pk(kAuth)) = blind(commit(yes, rAyes), bAyes)].0;
AyesP4 = out(C, unblind(xAyes, bAyes)).0;
AyesP5 = out(C, rAyes).0;

AnoP1 = out(A, sign(blind(commit(no, rAno), bAno), kA)).0;
AnoP2 = in(A, xAno).0;
AnoP3 = [check(xAno, pk(kAuth)) = blind(commit(no, rAno), bAno)].0;
AnoP4 = out(C, unblind(xAno, bAno)).0;
AnoP5 = out(C, rAno).0;


ByesP1 = out(B, sign(blind(commit(yes, rByes), bByes), kB)).0;
ByesP2 = in(B, xByes).0;
ByesP3 = [check(xByes, pk(kAuth)) = blind(commit(yes, rByes), bByes)].0;
ByesP4 = out(C, unblind(xByes, bByes)).0;
ByesP5 = out(C, rByes).0;

BnoP1 = out(B, sign(blind(commit(no, rBno), bBno), kB)).0;
BnoP2 = in(B, xBno).0;
BnoP3 = [check(xBno, pk(kAuth)) = blind(commit(no, rBno), bBno)].0;
BnoP4 = out(C, unblind(xBno, bBno)).0;
BnoP5 = out(C, rBno).0;

Ayes = AyesP1 :: AyesP2 :: AyesP3 :: AyesP4 :: AyesP5;
Ano = AnoP1 :: AnoP2 :: AnoP3 :: AnoP4 :: AnoP5;

Byes = ByesP1 :: ByesP2 :: ByesP3 :: ByesP4 :: ByesP5;
Bno = BnoP1 :: BnoP2 :: BnoP3 :: BnoP4 :: BnoP5;

AyesBno = Ayes || Bno;
AnoByes = Ano || Byes;

Setup = out(C, pk(kA)).out(C, pk(kB)).0;

P = Setup :: AyesBno;
Q = Setup :: AnoByes;

not equivalentft? P and Q;
