/* this examples illustrates why symbolic trace-to-symbolic trace
equivalence checking is not always complete. */

symbols enc/2,       // models encryption
  dec/2;             //    and decryption

channels c;

private a, b, k, ok;

var X, Y, Z;

rewrite dec(enc(X, Y), Y) -> X;

s = out(c, enc(a, k)).out(c, enc(b, k)).in(c, X).[enc(dec(X, k), k) = X].out(c, ok).0;

r1 = out(c, enc(a, k)).out(c, enc(b, k)).in(c, X).[X = enc(a, k)].out(c, ok).0;

r2 = out(c, enc(a, k)).out(c, enc(b, k)).in(c, X).[X = enc(b, k)].out(c, ok).0;

equivalentct? s and r1, r2; // s is equivalent to r1 and r2, but not to r1
// nor to r2
// as can be seen from the other examples running-example-first-trace.api
// and running-example-second-trace.api

