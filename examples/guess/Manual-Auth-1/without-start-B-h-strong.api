/*
   Manual authentication 1 
*/

symbols ready/0, start/0, h/2, pair/2, fst/1, snd/1;

weak k;
channels CA, CB;
privchannels OAH, OHA, OBH, OAB;
var X, Y, X1, Y1, XA1, XA2, XB1, XB2, XH1, XH2;

rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;

/* 1a.  ->A : d_A */
/* 1b.  ->B : d_B */
/* 2a. A=>H : ready */
/* 2b. B=>H : ready */
/* 3.  A=>B : (k,h(k,d_A)) */

Alice = in(CA,XA1).(out(OAH,ready) ||
        begin(XA1).
        (out(OAB,pair(k,h(k,XA1))) || out(CA,pair(k,h(k,XA1)))
        ));
        
Bob   = in(CB,XB1).(out(OBH,ready) ||
        in(OAB,XB2).[snd(XB2)=h(fst(XB2),XB1)].end(XB1)
        );

H     = (in(OAH,XH1).[XH1=ready] || in(OBH,XH2).[XH2=ready]);

P = (Alice || Bob || H);

correspondence? P;
