/*
   Manual authentication 1 

Security property: When A ends her session with d=XA1 then B has almost finished his session with XB1=XA1.
Scenario: 1 session of each role.
Result: No attack has been found.
*/

symbols ready/0, start/0, accept/0, h/2, bf/2, pair/2, fst/1, snd/1;

weak k;
channels CA, CB;
privchannels OAH, OHA, OBH, OAB;
var X,Y,X1,Y1,XA1,XA2,XA3,XB1,XB2,XH1,XH2,XH3;

rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;

/* 1a.  ->A : d_A */
/* 1b.  ->B : d_B */
/* 2a. A=>H : ready */
/* 2b. B=>H : ready */
/* 3a. A=>H : (k,h(k,d_A)) */
/* 3b. H=>B : (k,h(k,d_A)) */
/* 4a. B=>H : accept */
/* 4b. H=>A : accept */


Alice = in(CA,XA1).(out(OAH,ready) ||
        (out(OAB,pair(k,h(k,XA1))) || out(CA,pair(k,h(k,XA1))).
        in(OHA,XA3).[XA3=accept].end(XA1)
        ));
        
Bob   = in(CB,XB1).(out(OBH,ready) ||
        begin(XB1).
        in(OAB,XB2).[snd(XB2)=h(fst(XB2),XB1)].
        (out(OBH,accept) || 0
        ));

H     = (in(OAH,XH1).[XH1=ready] || in(OBH,XH2).[XH2=ready])::
        in(OBH,XH3).[XH3=accept].
        (out(OHA,accept) || 0
        );

P = (Alice || Bob || H);

correspondence? P;
