/*
   Manual authentication 1 
*/

symbols ready/0, start/0, accept/0, sh/2, bf/2, pair/2, fst/1, snd/1;

weak k;
channels CA, CB;
privchannels OAH, OHA, OBH, OAB;
var X,Y,X1,Y1,XA1,XA2,XA3,XB1,XB2,XH1,XH2,XH3;

rewrite sh(bf(Y, sh(X1,Y1)),Y) -> sh(X1,Y1);
rewrite sh(X,bf(X, sh(X1,Y1))) -> sh(X1,Y1);
rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;

/* 1a.  ->A : d_A */
/* 1b.  ->B : d_B */
/* 2a. A=>H : ready */
/* 2b. B=>H : ready */
/* 3.  H=>A : start */
/* 4.  A=>B : (k,sh(k,d_A)) */

Alice = in(CA,XA1).(out(OAH,ready) ||
        in(OHA,XA2).[XA2=start].
        (out(OAB,pair(k,sh(k,XA1))) || out(CA,pair(k,sh(k,XA1))).
        in(OHA,XA3).[XA3=accept].end(XA1)
        ));
        
Bob   = in(CB,XB1).(out(OBH,ready) ||
        begin(XB1).
        in(OAB,XB2).[snd(XB2)=sh(fst(XB2),XB1)].
        (out(OBH,accept) || 0
        ));

H     = (in(OAH,XH1).[XH1=ready] || in(OBH,XH2).[XH2=ready])::out(OHA,start)::
        in(OBH,XH3).[XH3=accept].
        (out(OHA,accept) || 0
        );

P = (Alice || Bob || H);

correspondence? P;
