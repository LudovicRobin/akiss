/*
   Manual authentication 4 (without message start)

Security property: When B ends his session with d=XB1 then A has almost finished (she has received start) his session with XA1=XB1.
Scenario: 1 session of each role.
Result: An attack has been found.  Indeed, message start is important to ensure that B has received
the commitment (msg 1) before A sends the values r and k.
*/

symbols h/3, ok/0, ready/0;

private k;
weak r;
channels CA, CB, CH;
privchannels OAH1, OAH2, OHA, OBH, OHB;
var X,Y, XA1, XA2, XA3, XB1, XB2, XB3, XB4, XH1, XH2, XH3, XH4;


/* 1. A->B  : d, h(d,k,r) */
/* 2. A=>H  : ready */
/* 3. B=>H  : ready */
/* 4. A=>H  : r */
/* 5. H=>B  : r */
/* 6. A->B  : k */
/* 7. B=>H  : ok */
/* 8. H=>A  : ok */


Alice = in(CA,XA1).begin(XA1).out(CA,h(XA1,k,r)).out(OAH1,ready).(out(OAH2,r)
		           	 			      || out(CA,r).out(CA,k).in(OHA,XA3).[XA3=ok]);

Bob = in(CB,XB1).in(CB,XB2).(out(OBH,ready)
                          || in(OHB,XB3).in(CB,XB4).[XB2=h(XB1,XB4,XB3)].out(OBH,ok).end(XB1));

H = (in(OAH1,XH1).[XH1=ready] || in(OBH,XH2).[XH2=ready])::in(OAH2,XH3).(out(OHB,XH3)
							            || out(CH,XH3).in(OBH,XH4).[XH4=ok].out(OHA,ok));

P = Alice || Bob || H;

correspondence? P;
