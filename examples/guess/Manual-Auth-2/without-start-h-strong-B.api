/*
   Manual authentication 2 (without start but h strong)

Security property: When B ends his session with d=XB1 then A has almost finished (she has received start) his session with XA1=XB1.
Scenario: 1 session of each role.
Result: No attack has been found.
*/

symbols hs/2, bruteforce/2, ok/0, pair/2, fst/1, snd/1, ready/0, start/0;

weak  k;
channels CA, CB, CH;
privchannels OAH1, OAH2, OHA1, OHA2, OBH1, OBH2, OHB;
var X,Y,X1,Y1, XA1, XA2, XA3, XB1, XB2, XB3, XB4, XH1, XH2, XH3, XH4;


rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;

/* 1a.  ->A  : dA */
/* 1b.  ->B  : dB */
/* 2a. A=>H  : ready */
/* 2b. B=>H  : ready */
/* 3.  A->B  : k */
/* 4a. A=>H  : k, hs(k,dA) */
/* 4b. B=>H  : k, hs(k,dB) */
/* 5a. H=>A : ok */
/* 5b. H=>B : ok */


Alice = in(CA,XA1).out(OAH1,ready).out(CA,k).(begin(XA1).out(OAH2,pair(k,hs(k,XA1)))
 		 				                       || out(CA,pair(k,hs(k,XA1))).in(OHA2,XA3).[XA3=ok]);

Bob = in(CB,XB1).(out(OBH1,ready)
               || in(CB,XB2).(out(OBH2,pair(XB2,hs(XB2,XB1)))
	                   || out(CB,pair(XB2,hs(XB2,XB1))).in(OHB,XB3).[XB3=ok].end(XB1)));


H = (in(OAH1,XH1).[XH1=ready] || in(OBH1,XH2).[XH2=ready])::
    (out(OHA1,start)
    || ((in(OAH2,XH3) || in(OBH2,XH4)) :: ([XH3=XH4].(out(OHA2,ok) || out(OHB,ok)))));
						    
P = Alice || Bob || H;

correspondence? P; 

