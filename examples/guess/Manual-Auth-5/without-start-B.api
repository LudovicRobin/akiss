/*
   Manual authentication 5 
*/

symbols ready/0, accept/0, sh/2, hash/1, bf/2;

private k;
channels CA,CB,CH;
privchannels OAH,OBH,OHA,OHB;
var X,Y,X1,Y1,XA1,XA2,XA3,XB1,XB2,XB3,XB4,XB5,XH1,XH2,XH3,XH4;

rewrite sh(bf(Y, sh(X1,Y1)),Y) -> sh(X1,Y1);
rewrite sh(X,bf(X, sh(X1,Y1))) -> sh(X1,Y1);

/* 1a.  ->A : d_A */
/* 1b.  ->B : d_B */
/* 2.  A->B : hash(k) */
/* 3a. A=>H : ready */
/* 3b. B=>H : ready */
/* 4.  A->B  : k */
/* 5a. A=>H : short(d,k) */
/* 5b. B=>H : short(d,k) */
/* 6.  H=>A : accept */
/* 6.  H=>B : accept */

Alice = in(CA,XA1).
        out(CA,hash(k)).
        (out(OAH,ready) ||
        begin(XA1).
        out(CA,k).
        (out(OAH,sh(XA1,k)) || out(CA,sh(XA1,k)).
        in(OHA,XA3).[XA3=accept]
        ));

Bob   = in(CB,XB1).
        in(CB,XB2).
        (out(OBH,ready) ||
        in(CB,XB3).
        [hash(XB3)=XB2].
        (out(OBH,sh(XB1,XB3)) || out(CB,sh(XB1,XB3)).
        in(OHB,XB4).[XB4=accept].end(XB1)
        ));

H     = (in(OAH,XH1).[XH1=ready] ||
        in(OBH,XH2).[XH2=ready]) ::
        (in(OAH,XH3) || in(OBH,XH4)) ::
        [XH3=XH4].
        (out(OHA,accept) || out(OHB,accept))
        ;

P = (Alice || Bob || H);

correspondence? P;
