/*
   Manual authentication 5 

Security property: When A ends her session with d=XA1 then B has almost finished his session with XB1=XA1.
Scenario: 1 session of each role.
Result: No attack has been found.
*/

symbols ready/0, start/0, accept/0, sh/2, hash/1, bf/2;

private k;
channels CA,CB,CH;
privchannels OAH,OBH,OHA,OHB;
var X,Y,X1,Y1,XA1,XA2,XA3,XB1,XB2,XB3,XB4,XB5,XH1,XH2,XH3,XH4;

rewrite sh(bf(Y, sh(X1,Y1)),Y) -> sh(X1,Y1);
rewrite sh(X,bf(X, sh(X1,Y1))) -> sh(X1,Y1);

/* 1a.  ->A : d_A */
/* 1b.  ->B : d_B */
/* 2.  A->B : hash(k) */
/* 3a. A=>H : ready */
/* 3b. B=>H : ready */
/* 4.  H=>A : start */
/* 5.  A->B : k */
/* 6a. A=>H : short(d,k) */
/* 6b. B=>H : short(d,k) */
/* 7a. H=>A : accept */
/* 7b. H=>B : accept */

Alice = in(CA,XA1).
        out(CA,hash(k)).
        (out(OAH,ready) ||
        in(OHA,XA2).[XA2=start].
        out(CA,k).
        (out(OAH,sh(XA1,k)) || out(CA,sh(XA1,k)).
        in(OHA,XA3).[XA3=accept].end(XA1)
        ));

Bob   = in(CB,XB1).
        in(CB,XB2).
        (out(OBH,ready) ||
        in(CB,XB3).
        [hash(XB3)=XB2].
        begin(XB1).
        (out(OBH,sh(XB1,XB3)) || out(CB,sh(XB1,XB3)).
        in(OHB,XB4).[XB4=accept]
        ));

H     = (in(OAH,XH1).[XH1=ready] ||
        in(OBH,XH2).[XH2=ready]) ::
        (out(OHA,start) ||
        (in(OAH,XH3) || in(OBH,XH4)) ::
        [XH3=XH4].
        (out(OHA,accept) || out(OHB,accept))
        );

P = (Alice || Bob || H);

correspondence? P;
