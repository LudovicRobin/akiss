/*
   Manual authentication 3 

Security property: When A ends her session with d=XA1 then B has almost finished his session with XB1=XA1.
Scenario: 1 session of each role.
Result: No attack has been found.
*/

symbols ready/0, accept/0, h/2, hash/1, bf/2;

private k;
channels CA,CB,CH;
privchannels OAH,OBH,OHA,OHB;
var X,Y,X1,Y1,XA1,XA2,XA3,XB1,XB2,XB3,XB4,XB5,XH1,XH2,XH3,XH4;

/* 1a.   ->A : d_A */
/* 1b.   ->B : d_B */
/* 2.   A->B : hash(k) */
/* 3a.  A=>H : ready */
/* 3b.  B=>H : ready */
/* 4a.  A=>H : h(d,k) */
/* 4b.  H=>B : h(d,k) */
/* 6.   A->B : k */
/* 7a.  B=>H : accept */
/* 7b.  H=>A : accept */


Alice = in(CA,XA1).
        out(CA,hash(k)).
        (out(OAH,ready) ||
        (out(OAH,h(XA1,k)) || out(CA,h(XA1,k)).
        out(CA,k).
        in(OHA,XA3).[XA3=accept].end(XA1)
        ));


Bob = in(CB,XB1).
      in(CB,XB2).
      (out(OBH,ready) ||
      in(OHB,XB4). //receive digest
      begin(XB1).
      in(CB,XB5).  // receive k
      [hash(XB5)=XB2].
      [h(XB1,XB5)=XB4].
      (out(OBH,accept)
      ));


H = (in(OAH,XH1).[XH1=ready] ||
    in(OBH,XH2).[XH2=ready]) ::
    in(OAH,XH3).
    (out(OHB,XH3) || out(CH,XH3).
    in(OBH,XH4).[XH4=accept]. 
    (out(OHA,accept) || 0
    ));

P = (Alice || Bob || H);

correspondence? P;
