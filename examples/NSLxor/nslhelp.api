// NSL with xor
// A -> B: aenc(<na,A>,pub(B))
// B -> A: aenc(<na+B,nb>,pub(A))
// A -> B: aenc(nb,pub(B))

// Well-known attack

#set xor;

symbols plus/2, zero/0, aenc/2, adec/2, pub/1, h/1,
        pair/2, fst/1, snd/1, yes/0, no/0, i/0;
private na, nb, a, b;
channels C;
var X,X1,X2,Y,Z;

rewrite adec(aenc(X,pub(Y)),Y) -> X;
rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;


Tinit = out(C,pub(a)).out(C,pub(b)).0;

// Static inequivalence scenario

r1 = Tinit :: out(C,aenc(pair(plus(na,plus(pub(b),pub(i))), pub(a)),pub(b))) :: out(C,yes);
r2 = Tinit :: out(C,aenc(pair(plus(na,plus(pub(b),pub(i))), pub(a)),pub(b))) :: out(C,no); 

inequivalentct? r1 and r2;

// Scenario with more explicit tests

RoleAIfst = out(C,aenc(pair(na,pub(a)),pub(i))).0;

RoleAIsnd =
  in(C,X).[plus(fst(adec(X,a)),pub(i)) = na].
  out(C,aenc(snd(adec(X,a)),pub(i))).0;

RoleAIsndhelp =
  in(C,X).[X= aenc(pair(plus(na,pub(i)),nb),pub(a))].
  out(C,aenc(snd(adec(X,a)),pub(i))).0;

RoleBfst =
  in(C,Y).out(C,aenc(pair(plus(fst(adec(Y,b)),pub(b)),nb),snd(adec(Y,b)))).0;

RoleBfsthelp =
  in(C,Y).[Y = aenc(pair(plus(na,plus(pub(b),pub(i))), pub(a)),pub(b))].
  out(C,aenc(pair(plus(fst(adec(Y,b)),pub(b)),nb),snd(adec(Y,b)))).0;


r1 = out(C,aenc(pair(plus(na,plus(pub(b),pub(i))), pub(a)),pub(b))) ::
     RoleBfsthelp :: RoleAIsndhelp :: out(C,yes);
r2 = out(C,aenc(pair(plus(na,plus(pub(b),pub(i))), pub(a)),pub(b))) ::
     RoleBfsthelp :: RoleAIsndhelp :: out(C,no); 

inequivalentct? r1 and r2;

// AKISS finds the attack:
// The following tests work on r1 but not on r2:
// check_identity([...],plus(w3,yes),zero)
// The following tests work on r2 but not on r1:
// check_identity([...],plus(w3,no),zero)
